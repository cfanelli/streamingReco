# scons build file for cMsg examples
Import('env', 'archDir', 'execLibs', 'debugSuffix')

# C & C++ files. Filter out all vxworks files (which start by convention with case-independent 'vx')
cfileList  = filter( (lambda a : a not in Glob('[vV][xX]*.c',  strings=True)), Glob('*.c',  strings=True))
ccfileList = filter( (lambda a : a not in Glob('[vV][xX]*.cc', strings=True)), Glob('*.cc', strings=True))

# Compile C++ files and install that executable if "scons install" is run
for fileName in cfileList:
    compiledFile = env.Program(source  = fileName,
                               LIBS    = ['cmsg'+debugSuffix, 'cmsgRegex'+debugSuffix] + execLibs)
    env.Install(target='#/bin/cMsg',source = compiledFile)

# Compile C++ files and install
for fileName in ccfileList:
    compiledFile = env.Program(source  = fileName,
                               LIBS    = ['cmsgxx'+debugSuffix,
                                          'cmsg'+debugSuffix,
                                          'cmsgRegex'+debugSuffix] + execLibs)
    env.Install(target='#/bin/cMsg',source = compiledFile)
    